# GENERATION
import:
	java.net.URL
	java.io.BufferedReader
	java.io.InputStreamReader

plural expression [get] (version) [(url)] %string%:
	return type: strings
	get:
		set {_link} to try new BufferedReader(try new InputStreamReader(try new URL(expr-1).openStream()))
		set {_q::*} to ...try {_link}.lines()
		try {_link}.close()
		return {_q::*}

on load:
	if plugin "skript-reflect" and "skript-yaml" and "Vault" is enabled:
		load yml "plugins/TablistManager/Config.yml" as "plugins/TablistManager/Config.yml"
		if yaml "plugins/TablistManager/Config.yml" is empty:
			set yaml value "Settings.Server_Prefix" of "plugins/TablistManager/Config.yml" to "&6&lSERVER"
			set yaml value "Settings.Update_Checker" of "plugins/TablistManager/Config.yml" to true
			set yaml value "Settings.Admin_Permission" of "plugins/TablistManager/Config.yml" to "tablistmanager.admin"
			set yaml value "Settings.Prefix" of "plugins/TablistManager/Config.yml" to false
			set yaml value "Settings.Suffix" of "plugins/TablistManager/Config.yml" to false
			set yaml value "Settings.Update_Time" of "plugins/TablistManager/Config.yml" to 5
			set yaml value "Settings.Tps_Performance_Value" of "plugins/TablistManager/Config.yml" to 12 #new
			set yaml value "Settings.Spectator_Hide" of "plugins/TablistManager/Config.yml" to true #new
			set yaml value "Blacklisted.Enabled" of "plugins/TablistManager/Config.yml" to false
			set yaml list "Blacklisted.Worlds" of "plugins/TablistManager/Config.yml" to "test-world" and "blacklisted-world"
			set yaml value "Content.Header" of "plugins/TablistManager/Config.yml" to "&d&lWelcome To My Server {newline} &c&oVisit the Config.yml to edit this!{newline}"
			set yaml value "Content.Footer" of "plugins/TablistManager/Config.yml" to "{nl}&e&lPlayers: &a{OnlinePlayers}/{MaxPlayers}, &7&oHello {player} {nl} &fTps: {Tps} &8| &fReal Time: {realtime} &r"
			save yml "plugins/TablistManager/Config.yml"
			send "&6&lTABLISTMANAGER &eA new Config.yml was created!" to console
		send "&6&lTABLISTMANAGER &eWas loaded correctly &8(&cNO ERRORS&8).%nl%&6&lTABLISTMANAGER WIKI: &ehttps://jacobjoergensen2.gitbook.io/tablistmanager/%nl%&6&lTABLISTMANAGER REPORT BUGS & IDEAS: &ehttps://github.com/JacobJoergensen/TablistManager/issues" to console
	else:
		send "&c&lADDON CHECKER &7You need the following Skript addons to run TablistManager: skript-reflect, skript-yaml and Vault" to console

on join:
	if player is op:
		if yaml value "Settings.Update_Checker" of "plugins/TablistManager/Config.yml" is true:
			if "%get version "https://raw.githubusercontent.com/JacobJoergensen/TablistManager/main/version.txt"%" is not "2.0":
				send "&c&lUPDATE CHECKER &7You are running an old version of TablistManager &8(&cCurrent version: &c2.0&8) - (&aLatest version: &a%get version "https://raw.githubusercontent.com/JacobJoergensen/TablistManager/main/version.txt"%&8) %nl% <link:https://github.com/JacobJoergensen/TablistManager/releases/latest><tooltip:Click here to download the latest version>&f&nDownload latest version<reset>%nl%"
	while player is online:
		load yml "plugins/TablistManager/Config.yml" as "plugins/TablistManager/Config.yml"
		if tps is bigger than "%yaml value "Settings.Tps_Performance_Value" of "plugins/TablistManager/Config.yml"%" parsed as number: #new
			# TABLIST UPDATER
			wait "%yaml value "Settings.Update_Time" of "plugins/TablistManager/Config.yml"% seconds" parsed as timespan
			TlmHandler(player, world of player)

on gamemode change: #new
	if yaml value "Settings.Spectator_Hide" of "plugins/TablistManager/Config.yml" is true: #new
		hide player from all players if player's gamemode is spectator #new
		reveal player to all players if player's gamemode is not spectator #new

# TABLIST HANDLER
function TlmHandler(p: player, w: world):
	load yml "plugins/TablistManager/Config.yml" as "plugins/TablistManager/Config.yml"
	if yaml value "Blacklisted.Enabled" of "plugins/TablistManager/Config.yml" is true: #new
		loop yaml list "Blacklisted.Worlds" of "plugins/TablistManager/Config.yml": #new
			if "%loop-val%" parsed as world = {_w}: #new
				set {_access} to false #new
				reset players' tab list header #new
				reset players' tab list footer #new
				set {_p}'s tab list name to "&7%{_p}'s displayname%" #new
	if {_access} is not false: #new
		set all players' tab list header to TlmPlaceholders("%yaml value "Content.Header" of "plugins/TablistManager/Config.yml"%", {_p})
		set all players' tab list footer to TlmPlaceholders("%yaml value "Content.Footer" of "plugins/TablistManager/Config.yml"%", {_p})
		load yml "plugins/TablistManager/Players.yml" as "plugins/TablistManager/Players.yml" #new
		if yaml value "%uuid of {_p}%.tabname" of "plugins/TablistManager/Players.yml" is not set: #new
			set yaml value "%uuid of {_p}%.tabname" of "plugins/TablistManager/Players.yml" to "§7%{_p}%" #new
			save yml "plugins/TablistManager/Players.yml" #new

		set {_prefix} to "%{_p}'s prefix% " if yaml value "Settings.Prefix" of "plugins/TablistManager/Config.yml" is true #new
		set {_prefix} to "" if yaml value "Settings.Prefix" of "plugins/TablistManager/Config.yml" is false #new
		set {_suffix} to " %{_p}'s suffix%" if yaml value "Settings.Suffix" of "plugins/TablistManager/Config.yml" is true #new
		set {_suffix} to "" if yaml value "Settings.Suffix" of "plugins/TablistManager/Config.yml" is false #new

		set {_tabName} to yaml value "%uuid of {_p}%.tabname" of "plugins/TablistManager/Players.yml" #new
		set {_p}'s tab list name to "%{_prefix}%%{_tabName}%%{_suffix}%" #new

# PLACEHOLDERS
function TlmPlaceholders(s: string, p: player) :: text:
	replace all "{NewLine}" or "{NL}" or "||" or "&&" with newline in {_s}
	replace all "{Player}" or "{PlayerName}" with "%{_p}%" in {_s}
	replace all "{DisplayName}" or "{PlayerDisplayName}" with {_p}'s displayname in {_s}
	replace all "{Level}" with "%{_p}'s level%" in {_s}
	replace all "{PlayerHealth}" with "%{_p}'s health%" in {_s}
	replace all "{MaxPlayerHealth}" with "%{_p}'s max health%" in {_s}
	replace all "{FoodLevel}" with "%{_p}'s food level%" in {_s}
	replace all "{Coordinate}" or "{Location}" with "%{_p}'s location%" in {_s}
	replace all "{Gamemode}" with "%{_p}'s gamemode%" in {_s}
	replace all "{LightLevel}" with "%light level at {_p}%" in {_s}
	replace all "{IpAddress}" with ip of {_p} in {_s}
	replace all "{TimePlayed}" with "%time played of {_p}%" in {_s}
	replace all "{World}" with "%{_p}'s world%" in {_s}
	replace all "{WorldTime}" with "%time in world ""%{_p}'s world%""%" in {_s}
	replace all "{OnlinePlayersInWorld}" or "{WorldOnline}" or "{Opiw}" with "%amount of all players in world ""%{_p}'s world%""%" in {_s} #new
	replace all "{Online}" or "{OnlinePlayers}" with "%number of all players%" in {_s}
	replace all "{StaffOnline}" or "{OnlineStaff}" with "%number of all players where [input has permission "tablistmanager.staff"]%" in {_s}
	replace all "{MaxPlayers}" with "%max player count%" in {_s}
	replace all "{BukkitVersion}" with bukkit version in {_s}
	replace all "{MinecraftVesion}" or "{McVersion}" with minecraft version in {_s}
	replace all "{Ping}" with "%{_p}'s ping%" in {_s}
	replace all "{Tps}" with "%tps%" in {_s}
	replace all "{ServerTime}" or "{RealTime}" with now formatted as "HH:mm:ss" in {_s}
	replace all "{Date}" with now formatted as "d/M/yyyy" in {_s}
	replace all "{Motd}" with motd in {_s}
	parse if plugin "CMI" or "Essentials" is enabled:
		replace all "{Money}" or "{Balance}" with "%{_p}'s balance%" in {_s}
	return {_s}

# COMMANDS
command /tablistmanager:tablistmanager [<text>] [<offline player>] [<text>]:
	aliases: tablist, tl, tm, tb, tbl, tablistmanager, tablist-manager, tlm, tblm, tab
	trigger:
		if player has permission "%yaml value "Settings.Admin_Permission" of "plugins/TablistManager/Config.yml"%":
			if arg 1 is not set:
				send formatted "&f%nl%&c▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬ &c&lTablist Help&c ▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬%nl%&f%nl%&6COMMAND OVERVIEW%nl%<tooltip:&b&oClick Here To Execute This Command><run command:/tablistmanager> &8➳ &e/Tablist &8, &7Shows this message. <reset>%nl%<sgt: /tablistmanager update> &8➳ &e/Tablist update <tooltip:&bExample: &7%player%>&8<&eplayer&8><reset> &8, &7Update the Tablist manually for a specific player. <reset>%nl%<sgt:/tablistmanager tabname %player% &&7%player%> &8➳ &e/Tablist tabname <tooltip:&bExample: &7%player%>&8<&eplayer&8> <tooltip:&bExample: &c&&cc%player%>&8<&etabname&8> <reset>&8, &7Change players tabname in the Tablist.<reset>%nl%<tooltip:&b&oClick Here To Execute This Command><run command:/tablistmanager reload> &8➳ &e/Tablist reload &8, &7Reload TablistManager.<reset>%nl%&f%nl%&f%nl%<tooltip:&ePlaceholders: &f{NewLine} &8| &f{Player} &8| &f{Displayname} &8| &f{Level} &8| &f{PlayerHealth} &8| &f{MaxPlayerHealth} &8| &f{FoodLevel} &8| &f{Coordinate} &8| &f{Gamemode} &8| &f{LightLevel} &8| &f{IpAddress} &8| &f{Timeplayed} &8| &f{World} &8| &f{WorldTime} &8| &f{OnlinePlayersInWorld} &8| &f{OnlinePlayers} &8| &f{StaffOnline} &8| &f{MaxPlayers} &8| &f{BukkitVersion} &8| &f{MinecraftVersion} &8| &f{Ping} &8| &f{Tps} &8| &f{ServerTime} &8| &f{Date} &8| &f{Motd} &8| &f{Money} &8➳ &cRequire Essentials or CMI >&6PLACEHOLDER HOVER LIST &8(&7Hover your mouse over here&8) <reset>%nl%&f%nl%&c▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬ &c&lTablist Help&c ▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬▬%nl%&f"
			if arg 1 is "help":
				execute player command "/tablist"
			if arg 1 is "reload":
				make player execute command "/sk reload TablistManager.sk"
				send formatted "&f%nl%&c&lTLM RELOAD &8➳ &aTablistManager was reloaded!%nl%&f%nl%<tooltip:&fClick Here To Visit Wiki><link:https://jacobjoergensen2.gitbook.io/tablistmanager/>&bVisit WIKI <reset>&8| <tooltip:&fClick Here To Report Bugs And Suggest Ideas><link:https://github.com/JacobJoergensen/TablistManager/issues>&bVisit REPORT BUGS & IDEAS<reset>"
			set {_player} to arg 2 if arg 2 is set #new
			set {_player} to player if arg 2 is not set #new
			if {_player} is set: #new
				if arg 1 is "update":
					TlmHandler({_player}, world of {_player})
					play sound "ENTITY_PLAYER_LEVELUP" with volume 2 to {_player}
					send title "&4&lTABLIST HELP" with subtitle "&8/&ctablist help" to {_player} for 2 seconds if {_player} has permission "%yaml value "Settings.Admin_Permission" of "plugins/TablistManager/Config.yml"%"
					send action bar "&eT&6a&eb&6l&ei&6s&et &6U&ep&6d&ea&6t&ee&6d" to {_player}
				if arg 1 is "tabname": #new
					set {_value} to arg 3 #new
					if {_value} is "reset" or "default": #new
						set {_value} to "&7%{_player}%" #new
					set yaml value "%uuid of {_player}%.tabname" of "plugins/TablistManager/Players.yml" to "%{_value}%" #new
					save yml "plugins/TablistManager/Players.yml" #new
					send formatted "&f%nl%&2&lEDIT: &aYou have changed &2%{_player}%'s&a TabName design,%nl% <tooltip:&b&oClick here to edit TabName for %{_player}%><sgt: /tablistmanager tabname %{_player}%>&8(&c&lPREVIEW: %{_value}%&8)<reset>%nl%&f" to player #new
					play sound "block.anvil.use" with volume 2 to {_player} #new
		else:
			send "%yaml value "Settings.Server_Prefix" of "plugins/TablistManager/Config.yml"% &cSorry you do not have access to this!"
